# Funktionen
In den vorhergehenden Kapiteln wurden schon einige Funktionen verwendet (z.B. `abs`, `typeof` oder `length`).
Eine Funktion wird durch ihren Namen, gefolgt von runden Klammern aufgerufen.
Innerhalb dieser runden Klammern stehen die *Argumente* der Funktion.
Vereinfacht lässt sich eine Funktion durch eine Box erklären, die mit Werten (den Argumenten) befüllt wird.
Innerhalb der Box wird dann Code ausgeführt und am Ende ein Ergebnis (*Return Value*) zurückgegeben.
Der Return Value kann einfach einer Variable zugewiesen werden:
```julia; term=true
l = length("spam")
```
Während manche Funktionen keine Argumente benötigen, erfordern andere Funktionen mehrere Eingangswerte.
```julia; term=true
pwd()
length("banana")
round(3.1415,digits=3)
```
`pwd` steht für *print working directory* und zeigt den aktuellen Arbeitspfad an.

Analog gibt es sowohl Funktionen die keinen *Return Value* zurückgeben, als auch Funktionen die mehrere Ergebnisse ausgeben.
```julia; term=true
p = println("Diese Zeile wird angezeigt, println weist p aber keinen Wert zu")
p
a = [10,35,4,65,9];
maximum(a)
findmax(a)
```
Der erste Aufruf von `println` schreibt zwar das Übergebene Argument in der REPL, die Ausgabe von `p` in der REPL zeigt allerdings keinen Wert an.
Das liegt daran, dass `println` keinen Return Value an die Variable `p` übergibt (genau genommen gibt die Funktion einen Wert vom Typ `Nothing` zurück).
In der nächsten Zeile wird der Array `a` erstellt.
Auf Arrays wird später noch genauer eingegangen.
Während `maximum` nur den Maximalwert von `a` zurückgibt, ermittelt `findmax` zusätzlich die Stelle des Maximalwertes.

Die Argumente einer Funktion können verschiedenste Ausdrücke sein.
Sowohl Werte und Variablen, als auch Funktionen können als Argument übergeben werden.
Julia wertet diese Ausdrücke dann aus und übergibt das Ergebnis an die Funktion.
```julia; term=true
s1 = "foo"; s2 = "bar";
string(s1, s2)
round(20/9)
length(pwd())
```

Manchen Funktionen können sogenannte *Keyword Arguments* übergeben werden.
`round` ist beispielsweise eine solche Funktion.
Mit dem *Keyword* `digits` gefolgt von `=` und einer Int-Zahl, kann die Anzahl der Nachkommastellen festgelegt werden.
```julia; term=true
round(2.3345323,digits=2)
```
Gerade bei Funktionen mit vielen (optionalen) Argumenten sind Keyword Arguments sehr hilfreich, da sie in beliebiger Reihenfolge an die Funktion übergeben werden können.

## Neue Funktionen erstellen
Vor allem für Operationen die oft wiederholt werden, ist es sinnvoll eigene Funktionen zu definieren; und zwar aus folgenden Gründen:
  - Das Debugging kann schrittweise in den einzelnen Funktionen erfolgen.
  - Änderungen im Code müssen nur in den einzelnen Funktionen umgesetzt werden.
  - Durch das Zusammenfassen von Operationen in Funktionen wird der Code besonders performant.
  - Sinnvoll konzipierte Funktionen für kleine Aufgaben sind vielseitig einsetzbar und können meist in mehreren Programmen verwendet werden.

Die Definition einer eigenen Funktion beginnt mit dem Keyword `function` am Zeilenanfang, gefolgt vom Funktionsnamen und den Argumenten in runden Klammern.
Danach folgen beliebig viele Zeilen Code und ein abschließendes `end`.
Funktionsnamen können wie Variablennamen gebildet werden.
Groß- und Kleinbuchstaben sind zulässig, es dürfen nahezu alle Unicode-Zeichen verwendet werden.
Der Name darf allerdings nicht mit einer Zahl beginnen oder einem von Julia verwendeten Keyword entsprechen.
Zusätzlich sollte ein Name nicht gleichzeitig für eine Funktion und eine Variable verwendet werden.
Bei jedem Aufruf der Funktion werden die Zeilen zwischen `function` und `end` ausgeführt und das Ergebnis der letzten Zeile als Return Value zurückgegeben.
Der Return Value einer Funktion kann aber auch mit dem Keyword `return` festgelegt werden.
```julia;results="hidden"
function my_square(x)
	println("Quadrat von $(x) wird berechnet...")  # Diese Zeile wird angezeigt
	return x^2  # Diese Zeile wird ausgeführt und als Return Value ausgegeben
end
```
```julia; term=true
my_square(3)
my_square(8)
```

Die Funktion `my_square` schreibt zuerst einen String in die REPL und gibt dann das Quadrat des übergebenen Arguments zurück.
Generell ist es in Julia nicht notwendig den Block innerhalb der Funktionsdefinition einzurücken, dadurch wird der Code allerdings wesentlich übersichtlicher.

Funktionen ohne Argumente können durch leere Klammern erstellt werden:
```julia;results="hidden"
function hello_world()
	println("Hello World")
end
```

Für die Definition besonders kurzer Funktionen eignet sich folgende Abkürzung:
```julia;results="hidden"
my_square(x) = x^2
```

Eine neue Funktion kann im Code erst verwendet werden, nachdem sie definiert wurde.
Da Julia ein Skript vom Beginn an Zeile für Zeile bis zum Ende ausführt, ist es daher sinnvoll die Funktionsdefinitionen an den Anfang des Skripts zu schreiben.

Im Abschnitt Hilfe wird beschrieben, dass Julia selbstdokumentiert ist.
Diese Dokumentation erfolgt durch sogenannte Docstrings direkt vor der Funktionsdefinition.
Ein Docstring wird durch drei Anführungszeichen begrenzt und kann folgendermaßen aussehen:
```julia;eval=false
"""
    my_square(x)

Schreibe "Quadrat von x wird berechnet..." in die REPL und gib das Quadrat von x zurück.
"""
function my_square(x)
    println("Quadrat von $(x) wird berechnet...")
    return x^2
end
```

Nach Ausführung dieses Code-Blocks wird in der Hilfe zu `my_square` das Docstring angezeigt.

Mehr Informationen zu Funktionen gibt es in der Dokumentation unter [Manual/Functions](https://docs.julialang.org/en/v1/manual/functions/).

## Scope
Die *Scope* einer Variable ist ein Bereich in dem diese Variable sichtbar ist.
In Julia gibt es die *Global Scope* und die *Local Scope*.
Die Definition einer Funktion erzeugt eine solche Local Scope.
Auf eine globale Variable kann auch in einer Local Scope zugegriffen werden.
Umgekehrt ist es nicht möglich in der Global Scope auf eine lokale Variable zuzugreifen:
```julia;results="hidden"
function f(x)
    a = x^2
end
```
```julia;term=true
a = 1;  # Globale Variable a erzeugen
f(3)  # In der Local Scope von f wird die lokale Variable a erzeugt
a  # Der Aufruf von f verändert die globale Variable a nicht
```
Selbst wenn in der Funktion `a` ein Wert zugewiesen wird, bleibt der Wert von a unverändert, da die Zuweisung in der ersten Zeile (`a = 1`) in der Global Scope geschieht und die zweite Zuweisung (`a = x^2`) in der Local Scope der Funktion ausgeführt wird.
Die beiden Variablen heißen zwar gleich, beeinflussen sich aber gegenseitig nicht.
Nachdem die Funktion ausgeführt wurde, werden alle Variablen der Local Scope dieser Funktion gelöscht und können nicht mehr aufgerufen werden.
Nur noch der Return Value bleibt im Speicher (sofern dieser einer Variable zugewiesen wird).
```julia; results="hidden"
function g(x)
    v = "fuer immer verloren"  # Lokale Variable v erzeugen
    x*2  # Return Value
end
```
```julia;term=true
a = g(3)
v
```

Mehr Informationen zu Scopes gibt es in der Dokumentation unter [Manual/Scope of Variables](https://docs.julialang.org/en/v1/manual/variables-and-scoping/).
## Methoden
Ein Wesentliches Feature von Funktionen Julia ist *Multiple Dispatch*.
Eine Funktion kann, abhängig davon welchen Datentyp die Argumente haben und wieviele Argumente übergeben werden, unterschiedliche Operationen ausführen.
```julia;term=true
my_mult(x::Number) = x*2  # Erste Methode von my_mult
my_mult(x::String) = x^2  # Zweite Methode von my_mult
my_mult(x::String,y::Int64) = x^y  # Dritte Methode von my_mult
my_mult(1.45)
my_mult("spam")
my_mult("foo",4)
```
Die Funktion `my_mult` hat nun 3 *Methoden*.
Die erste Methode ist für ein Argument vom Typ `Number` definiert.
Über `::` gefolgt vom Datentypen wird dieser dem Argument zugewiesen.
In Julia sind Datentypen in einer Hierarchie strukturiert.
Jeder Typ kann einen übergeordneten *Supertype* und/oder mehrere untergeordnete *Subtypes* haben.
Eine Methode für einen bestimmten Datentypen kann auch auf dessen Subtypen angewendet werden.
Eine für `Number` definierte Funktion kann daher auch auf alle möglichen Integer- und Float-Typen, aber auch komplexe und rationale Zahlen angewendet werden, da es sich dabei um Subtypen von `Number` (oder um Subtypen von Subtypen von Subtypen ... von `Number`) handelt.
Die zweite Methode wird angewendet wenn ein `String` an die Funktion übergeben wird und die dritte Funktion wird nur dann angewendet wenn zusätzlich zu einem `String` eine `Int64`-Zahl Argument ist. 

Abhängig von den eingegebenen Argumenten sucht Julia die passende Methode aus, wenn keine Methode für die Art und Anzahl von Argumenten definiert ist, gibt Julia einen *MethodError* zurück.

Mehr Informationen zu Methoden und Multiple Dispatch gibt es in der Dokumentation unter [Manual/Methods](https://docs.julialang.org/en/v1/manual/methods/).
## Aufgaben
### Dreimal printen
Schreibe eine Funktion `print_three`, die ein beliebiges Argument dreimal in einer Zeile printed und dabei einen Abstand zwischen den Argumenten einfügt.
```julia;echo=false;results="hidden"
function print_three(a)
    println(string(a," ",a," ",a))
end
```
```julia;term=true
print_three("spam")
print_three(19)
```

### Zwei Funktionen verketten
Schreibe eine Funktion `verkette(f,g,x)`, die die beiden Funktionen `f` und `g` verkettet und für `x` auswertet (`f(g(x))`).
Die Ausgewertete Funktion soll als Return Value zurückgegeben werden.
```julia;echo=false;results="hidden"
function verkette(f, g, x)
    return f(g(x))
end
```
```julia;term=true
f(x) = x+2
g(x) = x^2
verkette(f,g,3)
verkette(length,g,"spam")
``` 

### Kreisfläche berechnen
Schreibe eine Funktion `kreis_flaeche(r)`, die den Radius eines Kreises als Argument benötigt und dann die Fläche dieses Kreises als Return Value zurückgibt.
```julia;echo=false;results="hidden"
kreis_flaeche(r) = r^2*π
```
```julia;term=true
kreis_flaeche(3)
kreis_flaeche(2.57)
```

### Kreisumfang berechnen
Schreibe eine Funktion `kreis_umfang(r)`, die den Radius eines Kreises als Argument benötigt und dann den Umfang dieses Kreises als Return Value zurückgibt.
```julia;echo=false;results="hidden"
kreis_umfang(r) = 2*r*π
```
```julia;term=true
kreis_umfang(3)
kreis_umfang(2.57)
2 * kreis_flaeche(5)/kreis_umfang(5)
```

### Kreiseigenschaften printen
Schreibe eine Funktion `kreis(r)`, die den Radius eines Kreises als Argument benötigt und Radius, Fläche und Umfange dieses Kreises auf zwei Nachkommastellen gerundet printed.
```julia;echo=false;results="hidden"
function kreis(r)
    println("r = $(round(r,digits=2))")
    println("A = $(round(kreis_flaeche(r),digits=2))")
    println("U = $(round(kreis_umfang(r),digits=2))")
end
```
```julia;term=true
kreis(3)
```
